//*.......  JOB  ....YOUR JOBCARD......
//*
//ASM      EXEC PGM=ASMA90,PARM='OBJECT,NODECK,LINECOUNT(64)'
//SYSPRINT DD SYSOUT=*
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD DUMMY
//SYSUT1   DD UNIT=VIO,SPACE=(1024,(120,120))
//SYSLIN   DD UNIT=VIO,SPACE=(3040,(40,40),RLSE),
//            DCB=(RECFM=FBS,LRECL=80,BLKSIZE=0),
//            DSN=&&SYSLIN,DISP=(NEW,PASS)
//SYSIN    DD *
         TITLE 'SYNCHROUNOUS LLA UPDATE ROUTINE'
* SYNCHROUNOUS LLA UPDATE ROUTINE (USING "LLACOPY" MACRO)
*
* SPECIFICATIONS:
*
* MODULE NAME: LLAUPDTX
*
* ATTRIBUTES : AC=1
*
* FUNCTION = SEE BELOW
*
*     THIS ROUTINE INVOKES THE "LLACOPY" MACRO TO SYNCHRONOUSLY
*  REFRESH AN INDIVIDUAL (MEMBER) LLA ENTRY.
*  "LLACOPY" HAS TO BE INVOKED IN SUPERVISOR STATE, KEY 0.
*
*
*  INPUT TO THE ROUTINE:
*
*       . "LLAPDS" DD SPECIFYING LLA MANAGED PDS IN WHICH THERE IS
*         AN UPDATED MEMBER.
*
*       . JCL PARM FIELD IN THE FOLLOWING FORMAT: XXYYYYYYYY
*         IF "XX" SPECIFIES "LL" IT MEANS THAT AN LINKLIST MEMBER IS
*         TO BE REFRESHED. "LLAPDS" DD IS NOT REQUIRED IN SUCH A CASE.
*         ANY OTHER VALUE OF "XX" IS IGNORED AND "LLAPDS" DD IS
*         REQUIRED THEN.
*         "YYYYYYYY" IS THE MEMBER NAME WHICH LLA DIRECTORY ENTRY
*         NEEDS TO BE UPDATED. MEMBER NAME IS REQUIRED.
*         USE "LLAUPDTE" PROGRAM TO REFRESH THE WHOLE DIRECORY OF
*         A PDS.
*
*
*  OUTPUT:
*
*       . A RETRUN CODE FROM THE "LLACOPY" MACRO.
*         0 - NORMAL, SUCCESSFUL COMPLETION.
*         4 - LLACOPY DID NOT FIND THE SPECIFED MEMBER IN THE "LLAPDS".
*             ENTRY FOR THAT MEMBER WAS REMOVED FROM LLA (IF PRESENT).
*         8 - I/O ERROR ACCESSING "LLAPDS"
*        16 - PARM FIELD IS INVALID
*
*
*  SAMPLE JCL TO RUN THIS PROGRAM:
*
*         //LLAUPDTX PROC LL=XX
*         //LLAUPDTX EXEC PGM=LLAUPDTX,PARM='&LL.&MEMBER.'
*         //STEPLIB    DD DISP=SHR,DSN=APF.PROG.LIB
*         //LLAPDS     DD DISP=SHR,DSN=&LLAPDS
*         //         PEND
*         //  EXEC LLAUPDTX,LL=LL,MEMBER=PROGRAMX, LINKLST UPDTE
*         //                LLAPDS='SYS1.LINKLIB'
*         //  EXEC LLAUPDTX,MEMBER=PROGRAMY, A USER LIBRARY UPDTE
*         //                LLAPDS='SYS2.LINKLIB'
*
*
* WRITTEN BY: JANEK JAKUBEK, OGL
*             JUNE      1991
*
* CHANGE ACTIVITY:
*                 NONE
*
* REGISTERS EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RDCB     EQU   R9                 DCB ADDRESS REGISTER
RCVT     EQU   R8                 CVT ADDRESS REGISTER
         SPACE
*   BITS EQUATES
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
         SPACE
         PRINT NOGEN
LLAUPDTX CSECT                    CSECT NAME
         USING LLAUPDTX,R15
         SAVE  (14,12)            SAVE REGISTERS
         CNOP  0,4                ALIGNEMENT ON A FULLWORD BOUNDARY
         BAL   R2,SAE             SAVE AREA ADDRESS
SA       DS    18F                SAVE AREA
         USING SA,R2
SAE      ST    R13,SA+4           CHAIN
         ST    R2,8(R13)          SAVE AREAS
         LR    R13,R2             CURRENT SAVE AREA
         DROP  R2
         USING SA,R13             PROGRAM BASE REGISTER
         SPACE
         L     R1,0(,R1)          LOAD ADDRESS OF THE PARM FIELD
         CLC   0(2,R1),=H'3'      IS LL AND MEMBER NAME SPECIFIED.?
         BL    PARMERR1           NO, PARM TOO SHORT
         MVC   LL,2(R1)           SAVE THE LINKLIST INDICATOR
         LH    R2,0(,R1)          LENGTH OF THE PARM FIELD INTO R2
         SH    R2,=H'2'           - 2 (LL) = MEMBER NAME LENGTH
         CH    R2,=H'8'           IS MEMBER NAME LENGTH > 8...........?
         BNH   PARMP1             NO, SAVE MEMBER NAME --------------->
         LH    R2,=H'8'           YES, USE ONLY FIRST 8 BYTES
PARMP1   BCTR  R2,0               MEMBER NAME LENGTH - 1
         MVC   MEMNAME,=CL9' '    CLEAR THE MEMBER NAME FIELD
         EX    R2,MVCMEMN         MOVE MEMBER NAME INTO MEMNAME FIELD
         SPACE
* CHECK IF LINKLIST MEMBER UODATE
         CLC LL,=C'LL'            LINKLST UPDATE .....................?
         BNE   NOTLNKL            NO, LOAD OUR DCB ADDRESS------------>
* A LNKLST MEMBER UPDATE, USE LINK LIST DCB ADDRESS
         L     RCVT,16            LOAD CVT ADDRESS
         USING CVT,RCVT           SET CVT ADDRESSABILITY
         L     RDCB,CVTLINK       LOAD LNKLST DCB ADDRESS
         B     LLACOPY            ISSUE LLACOPY MACRO
NOTLNKL  LA    RDCB,LLAPDS        LOAD OUR DCB ADDRESS
         OPEN  (LLAPDS,(INPUT))   DCB FOR LLACOPY HAS TO BE OPENED
         SPACE
* ENTER SUPERVISOR STATE, KEY 0 FIRST
*
LLACOPY  MODESET MODE=SUP,KEY=ZERO
         LLACOPY BLDLLIST=BLDL,DCB=(RDCB),                             +
               RETCODE=RETNCODE,RSNCODE=RSONCODE
         MODESET MODE=PROB,KEY=NZERO   RESTORE THE PROBLEM STATE
         CLC   LL,=C'LL'          LINKLST UPDATED.....................?
         BE    LLACPYX            YES, NO CLOSE REQUIRED-------------->
         CLOSE (LLAPDS)           NO, CLOSE THE DCB FIRST
LLACPYX  L     R15,RETNCODE       LOAD RETURN CODE FROM LLACOPY
RET      L     R13,SA+4           PREVIOUS SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)
         SPACE
RC16     LA    R15,16             UNSUCCESSFUL COMPLETION
         B     RET
         SPACE
MVCMEMN  MVC   MEMNAME(1),4(R1)   MOVE MEMBER NAME FROM THE PARM FIELD
         SPACE
PARMERR1 DS    0H                 INVALID PARM FIELD
         WTO   'LLAUPDTX - INVALID PARM FIELD'
         B     RC16
*  LLAPDS DATA SET
LLAPDS   DCB   DSORG=PO,MACRF=R,DDNAME=LLAPDS
         SPACE
LL       DS    CL2                LL FROM JCL PARM
RETNCODE DS    F                  RETURN CODE FROM LLACOPY MACRO
RSONCODE DS    F                  REASON CODE FROM LLACOPY MACRO
*//////////////////////////////////////////////////////////////////////
BLDL     DS    0F                 BLDL LIST TO FOR LLACOPY
         DC    AL2(1)             NUMBER OF ENTRIES
         DC    AL2(12)            ENTRY LENGTH (MINIMUM IS 12 BYTES)
MEMNAME  DS    CL8                MEMBER NAME TO BE REFRESHED IN LLA
         DS    CL4                TTR,K FIELDS TO BE RETURNED
*//////////////////////////////////////////////////////////////////////
         LTORG
         SPACE
         CVT   DSECT=YES          CVT MAPPING MACRO
         END   LLAUPDTX
/*
//*
//LKED     EXEC PGM=IEWL,
//         PARM='NCAL,LET,LIST,XREF,AC=1'
//SYSLMOD  DD DISP=SHR,DSN=...YOUR.APF.LOADLIB....
//SYSUT1   DD UNIT=VIO,SPACE=(TRK,(10,10))
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=&&SYSLIN,DISP=(OLD,DELETE)
//         DD DDNAME=SYSIN
//SYSIN    DD *
 NAME LLAUPDTX(R)
/*
