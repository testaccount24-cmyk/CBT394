/*.REXX EXEC */
/*.*******************************************************************/
/*.LDAPROBE : Issue ldapsearch command every  5  minute/s            */
/*            Issue a highlighted message if command fails           */
/*                                                                   */
/* Can be stopped/ terminated via "STOP jobname" MVS command.        */
/*                                                                   */
/* Needs a userid with OMVS segment to run.                          */
/* Needs to be customized to querry LDAP servers in your environment.*/
/*                                                                   */
/* External subroutines used:                                        */
/* DELAY from L.Woren                                                */
/* WTOH - my own (Janek's)                                           */
/*                                                                   */
/* Written by: Janek Jakubek, CGI                                    */
/*             January  2003                                         */
/*                                                                   */
/* Acknowledgement:                                                  */
/* Some of the code is borrowed from LDAPMAIL written by             */
/* Lionel B. Dyck.                                                   */
/*.*******************************************************************/
/*.FORMAT: LDAPROBE                                                  */
/*.*******************************************************************/
TRACE OFF
ARG PARMS
stdout  = "LDAPMSGS"
stderr  = "LDAPERRS"
jobname = MVSVAR('SYMDEF',JOBNAME)
msgid   = SYSVAR('SYSICMD')
EXITRC  = 0
SECONDS = 300
/* Issue LDAP search command  every 5 minutes (300 secs) */
DO FOREVER
  /* ----------------------------------------------------- *
   * Now invoke the ldap search command.                   *
   * ----------------------------------------------------- */
   /* ----------------------------------------------------- *
    * Set defaults                  *custom*                *
    *                                                       *
    *  ldap_s is the host name of the ldap server           *
    *  ldap_o are data to be returned by ldapsearch         *
    * ----------------------------------------------------- *
    * Examples:                                             *
    * ldap_s     = "mailhub.kp.org"                         *
    * ldap_o     = "o=Kaiser Permanente,c=US"               *
    * ----------------------------------------------------- */
   ldap_o     = "dc=xxxxxxxxxxx,dc=ca"
   /* *custom*: the 3 lines below are specific to every LDAP   */
   /*           sever being monitored                          */
   /* */
   ldapsrvn   = "LDAPstc1"         /* prod LDAP server STC name*/
   /* Below, specify LDAP server IP addr or host name          */
   ldap_s     = "xxx.xx.xx.xxx -p 389"
   CALL LDAPsrch
   /* */
   ldapsrvn   = "LDAPstc2"         /* test LDAP server STC name*/
   /* Below, specify LDAP server IP addr or host name          */
   ldap_s     = "xxx.xx.xx.xxx -p 389"
   CALL LDAPsrch
  /* CALL L.Woren DELAY subroutine               */
  /* SAY 'READY TO SLEEP FOR ' SECONDS 'SECONDS' */
   "DELAY "SECONDS
   If Rc = 8 Then      /* Operator STOP has been issued */
     do
     say msgid 'Operator STOP has been issued, terminating ...'
     exit   0;
     end

END
/* */
EXIT:
EXIT EXITRC
/* */
LDAPsrch:
cmd = 'GLDSRCH ENVAR("LIBPATH=/")' ,
      '/ -h' ldap_s '-b "'ldap_o'"' ,
      '"dc=ca"' ,
      ">DD:"stdout "2>DD:"stderr
cmd
If Rc Â¬= 0 Then
  do
  say msgid  ldapsrvn 'stc: Ldapsearch return code:' rc
  errmsg = "LDAP server "||ldapsrvn||" not responding, "
  errmsg = errmsg||"review "||stderr" /investigate"
  say errmsg
  /* Call JanekJ's WTOH subroutine               */
  /* Issue a highlighted message to console      */
  "WTOH "errmsg
  end
RETURN
