//jobname  JOB ....your jobcard
//*
/*ROUTE PRINT LOCAL
//ASM     EXEC PGM=ASMA90,PARM='NODECK,OBJ'
//*STEPLIB   DD DISP=SHR,DSN=SYS1.SASMMOD1
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=SYS1.MODGEN
//SYSUT1    DD UNIT=SYSALLDA,SPACE=(CYL,(1,1))
//SYSPRINT  DD SYSOUT=*
//SYSLIN    DD DSN=&&OBJECT,SPACE=(CYL,(1,1)),UNIT=SYSALLDA,
//             DISP=(NEW,PASS),
//             DCB=(BLKSIZE=0,LRECL=80,RECFM=FBS)
//SYSIN     DD *
         TITLE 'WRITE a highlighted MESSAGE TO OPERATOR ROUTINE'
* SPECIFICATIONS:
*
* MODULE NAME: WTOH
*
* ATTRIBUTES : REUSABLE
*
* FUNCTION = SEE BELOW
*
*     THIS ROUTINE WRITES MESSAGE(S) TO THE OPERATOR. MESSAGE IS
*  HIGHLIGHTED AND WILL STAY ON THE CONSOLE'S SCREEN UNTIL DELETED BY
*  THE OPERATOR. THE PROGRAM IS INTENDED TO BE USED TO INFORM
*  OPERATIONS ABOUT AN IMPORTANT CONDITION REQUIRING THEIR IMMEDIATE
*  ATTENTION.
*     MESSAGE TEXT CAN BE PASSED EITHER IN PARM FIELD OR IN SYSIN FILE.
*  SYSIN FILE HAS TO HAVE 80 BYTES RECORDS. CONTENTS OF COLUMNS 1-72 OF
*  EACH SYSIN RECORD IS WRITTEN AS SEPARATE "WTO" MESSAGE. THE ROUTINE
*  WILL DISPLAY UP TO MAX OF 10 RECORDS FROM SYSIN IN ORDER TO AVOID
*  FLOODING OF THE OPERATOR CONSOLE WITH MESSAGES FROM PROBLEM
*  PROGRAMS.  BOTH PARM AND SYSIN INPUTS ARE OPTIONAL. IF BOTH ARE
*  SPECIFIED, TEXT FROM PARM WILL BE DISPLAYED AS THE FIRST MESSAGE.
*
*    EACH WTO MESSAGE HAS JOB NAME PREFIXED TO IT.
*
*    THIS ROUTINE CAN BE EITHER INVOKED FROM JCL OR DYNAMICALLY THROUGH
*  MEANS OF LINK OR LOAD/CALL MACROS. CALL FORMAT IS THE FOLLOWING :
*
*   CALL WTOH,(parm {,ddname}),VL
*
*        "parm" ARGUMENT HAS TO HAVE THE SAME FORMAT AS OS OPERATING
*               SYSTEM STANDARD.
*        "ddname" ARGUMENT IS OPTIONAL AND USER CAN SPECIFY IN IT
*               DDNAME OF SYSIN MESSAGE INPUT FILE. SPECIFYING BLANK
*               "ddname" PREVENTS WTOH FROM PROCESSING MESSAGE FILE
*               AT ALL.
*
* SAMPLE EXECUTION FROM JCL :
*
*   //WTOH   PROC
*   //WTOH   EXEC PGM=WTOH,PARM='&MSG.'
*   //       PEND
*   //       EXEC WTOH,MSG='UPDATE OF "PRODUCE" MASTER FILE FAILED'
*   //SYSIN    DD *
*   IDENTIFY AND CORRECT THE ERRORS AND DO THE RERUN PROCEDURE
*   /*
*
* WRITTEN BY: Janek JAKUBEK, OGL
*             FEBRUARY  1985
*
* CHANGE ACTIVITY:
*                 NONE
*
*        PRINT NOGEN
WTOH     CSECT
WTOH     AMODE 24
WTOH     RMODE 24
         SAVE  (14,12)            SAVE REGISTERS
         USING WTOH,R15
         SPACE
* REGISTERS EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
*   BITS EQUATES
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
         CNOP  0,4                ALIGNEMENT ON A FULLWORD BOUNDARY
         BAL   WRKR1,WTOHSAE      SET SAVE AREA ADDRESS
WTOHSA   DS    18F                SAVE AREA
WTOHSAE  ST    R13,WTOHSA+4       CHAIN
         ST    WRKR1,8(R13)       SAVE AREAS
         LR    R13,WRKR1          CURRENT SAVE AREA
         DROP  R15
         USING WTOHSA,R13         PROGRAM BASE REGISTER
         SPACE
WRKR1    EQU   R2
WRKR2    EQU   R3
WRKR3    EQU   R4
TIOTR    EQU   R5
LNKREG   EQU   R6
         SPACE
*  CHECK IF SYSIN DDNAME SPECIFIED
         SPACE
         MVC   SYSIN+40(8),DDNSYSIN DEFAULT SYSIN DDNAME INTO DCB
         USING PARMLST,R1
         TM    PARMA,BIT0         LAST ENTRY IN PARM LIST.............?
         BO    NODDN              YES, NO DDNAME SPECIFICATION-------->
         L     WRKR1,DDNAMEA      DDNAME ADDRESS
         MVC   SYSIN+40(8),0(WRKR1) CHANGE DDNAME IN DCB
NODDN    L     WRKR1,PARMA        PARM ADDRESS
         DROP  R1
         SPACE
* GET JOB NAME AND PUT IT INTO MESSAGE TEXT
         SPACE
         EXTRACT TIOTA,'S',FIELDS=(TIOT) EXTRACT JOB NAME INFO
         L     TIOTR,TIOTA        TIOT ADDRESS
         USING TIOT,TIOTR         TIOT ADDRESSABILITY
         MVC   MSGJN,JOBN         MOVE JOB NAME INTO MESSAGE TEXT
         SPACE
* PARM FIELD PROCESSING
         SPACE
         MVI   FRAME,X'00'        NO MESSAGE FRAME
         USING PARM,WRKR1
         MVC   MSGL,PLEN          MOVE TO HALFW.IF NOT ON HALFW.BNDRY
         LH    WRKR2,MSGL         PARM FIELD LENGTH
         LTR   WRKR2,WRKR2        IS PARM SPECIFIED ..................?
         BZ    SYSINP             NO, GOTO SYSIN PROCESSING----------->
         LA    WRKR3,MSGT(WRKR2)  ADDRESS OF DESCRIPTOR CODES
         MVC   0(2,WRKR3),DESCDE  MOVE DESCRIPTOR CODES AFTER MSG TEXT
         MVC   2(2,WRKR3),ROUTCDE MOVE ROUTING CODES AFTER MSG DESCDE
         BCTR  WRKR2,0            PARM LENGTH - 1
         EX    WRKR2,MVCPARM      MOVE PARM INTO MESSAGE TEXT
         LA    WRKR2,14(0,WRKR2)  MESSAGE LENGTH
         STH   WRKR2,MSGL         COMPLETE LIST WTOH
         BAL   LNKREG,MSGFRAME    START OF MESSAGE FRAME
         WTO   MF=(E,MSG)         WRITE MESSAGE
         SPACE
* SYSIN PROCESSING
         SPACE
SYSINP   DS    0H
         CLI   SYSIN+40,X'40'     BLANK DDNAME         ...............?
         BE    RET                YES, DO NOT PROCESS MESSAGE FILE---->
         RDJFCB MF=(E,SYSINOPN)   READ JFCB MACRO
         LTR   R15,R15            JFCB READ SUCCESSFULLY..............?
         BNZ   RET                NO, SYSIN DD NOT SPECIFIED---------->
         OPEN  MF=(E,SYSINOPN)    OPEN SYSIN
         LA    WRKR1,10           MAX NO. OF MESSAGES IN SYSIN
GETMSG   GET   SYSIN,MSGT         GET MESSAGE RECORD
         LA    WRKR2,71           CHECK OF MESSAGE TEXT LENGTH
CHKMSGL  LA    WRKR3,MSGT(WRKR2)  LAST MSG CHAR ADDDRESS
         CLI   0(WRKR3),X'40'     BLANK                ...............?
         BNE   WTOHMSG            NO, LAST CHAR OF MESSAGE------------>
         BCT   WRKR2,CHKMSGL      CHECK NEXT CHARACTER
WTOHMSG  LA    WRKR2,14(0,WRKR2)  MESSAGE LENGTH
         STH   WRKR2,MSGL         INTO WTOH LIST
         LA    WRKR3,MSGL(WRKR2)  ADDRESS OF DESCRIPTOR CODES
         MVC   0(2,WRKR3),DESCDE  MOVE DESCRIPTOR CODES AFTER MSG TEXT
         MVC   2(2,WRKR3),ROUTCDE MOVE ROUTING CODES AFTER MSG DESCDE
         CLI   FRAME,X'00'        START OF MESSAGE FRAME DONE.........?
         BNE   WTOHMSG1           YES, DON'T DO IT AGAIN-------------->
         BAL   LNKREG,MSGFRAME    START OF MESSAGE FRAME
WTOHMSG1 WTO   MF=(E,MSG)         WRITE MESSAGE
         BCT   WRKR1,GETMSG       GET NEXT MESSAGE RECORD
         SPACE
EOF      CLOSE (SYSIN)
RET      CLI   FRAME,X'00'        START OF MESSAGE FRAME DONE.........?
         BE    RET1               NO, NO END OF FRAME NECESSARY------->
         BAL   LNKREG,MSGFRAME    END OF MESSAGE FRAME
RET1     L     R13,WTOHSA+4       PREVIOUS SAVE AREA ADDRESS
         RETURN (14,12),RC=0
         SPACE
MVCPARM  MVC   MSGT(1),PFLD       MOVE PARM FIELD INTO MSG TEXT
         SPACE
MSGFRAME DS    0H                 WTO MESSAGE FRAME
         CLI   FRAME,X'FF'        START OF FRAME ALREADY DONE.........?
         BE    MSGFRME1           YES, WRITE END OF FRAME------------->
         WTO   '*******************************************************+
               ****************',ROUTCDE=(2),                          +
               DESC=(6)                               START OF FRAME
         MVI   FRAME,X'FF'        START OF MESSAGE FRAME FLAG
         BR    LNKREG             RETURN TO CALLER
MSGFRME1 DS    0H                 END OF MESSAGE FRAME
         WTO   '* * * * * * * * * * * * * * * * * * * * * * * * * * * *+
                * * * * * * * *',ROUTCDE=(2),                          +
               DESC=(6)                               END OF FRAME
         BR    LNKREG             RETURN TO CALLER
         SPACE
DDNSYSIN DC    CL8'SYSIN'         DEFAULT SYSIN DDNAME
MSG      DS    0F                 WTO LIST FORM
MSGL     DS    H                  MESSAGE LENGTH
MCSF     DC    B'1000000000000000' MCS FLAGS
MSGJN    DS    CL8                JOB NAME
         DC    C' '
MSGT     DS    CL104              MESSAGE TEXT
FRAME    DS    XL1                FRAME OF MESSAGES FLAG
DESCDE   DC    B'0000000000100000' DESC=(11)   DESCRIPTOR CODE
ROUTCDE  DC    B'0100000000000000' ROUTCDE=(2) ROUTING CODE
         SPACE
TIOTA    DS    F                  TIOT ADDRESS
         SPACE
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOF,               +
               RECFM=FB,LRECL=80,EXLST=JFCBEXL
         SPACE
JFCBEXL  DS    0F                 JFCB PROCESSING EXIT
         DC    X'87',AL3(JFCBAREA)
         SPACE
JFCBAREA DS    0F,CL176
SYSINOPN OPEN  (SYSIN,(INPUT)),MF=L OPEN SYSIN LIST FORM
         SPACE
PARMLST  DSECT                    PARAMETER LIST
PARMA    DS    A                  PARM FIELD ADDRESS
DDNAMEA  DS    A                  SYSIN DDNAME ADDRESS ( OPTIONAL )
         SPACE
TIOT     DSECT                    TASK INPUT OUTPUT TABLE
JOBN     DS    CL8                JOB NAME
         SPACE
PARM     DSECT                    PARM FIELD LAYOUT
PLEN     DS    H                  FIELD LENGTH
PFLD     DS    CL100              PARM FIELD TEXT
         END   WTOH
/*
//LINK    EXEC PGM=HEWL,PARM='MAP,LET,LIST,REUS'
//SYSLIN    DD DSN=&&OBJECT,DISP=(OLD,DELETE)
//SYSLMOD   DD DISP=SHR,DSN=...your.loadlib    <=== DESTINATION
//SYSUT1    DD UNIT=SYSALLDA,SPACE=(CYL,(1,1))
//SYSPRINT  DD SYSOUT=*
//*
//* Optional step to refresh LLA entry (if SYSLMOD is LLA managed)
//*
//* LINKLIST MEMBER
//LLAUPDTX PROC LL=LL,LLAPDS='SYS1.LINKLIB'
//* NON-LINKLIST MEMBER
//*LLAUPDTX PROC LL=AA,LLAPDS='YOUR.LLA.DSNAME'
//LLAUPDTX EXEC PGM=LLAUPDTX,PARM='&LL.&MEMBER.'
//LLAPDS     DD DISP=SHR,DSN=&LLAPDS
//SYSUDUMP   DD SYSOUT=D
//         PEND
//  EXEC LLAUPDTX,LL=LL,MEMBER=WTOH
