//*.......  JOB  ....YOUR JOBCARD......
//*
//ASM      EXEC PGM=ASMA90,PARM='OBJECT,NODECK,LINECOUNT(64)'
//SYSPRINT DD SYSOUT=*
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD DUMMY
//SYSUT1   DD UNIT=VIO,SPACE=(1024,(120,120))
//SYSLIN   DD UNIT=VIO,SPACE=(3040,(40,40),RLSE),
//            DCB=(RECFM=FBS,LRECL=80,BLKSIZE=0),
//            DSN=&&SYSLIN,DISP=(NEW,PASS)
//SYSIN    DD *
         TITLE 'SYNCHROUNOUS LLA UPDATE ROUTINE'
* SYNCHROUNOUS LLA UPDATE ROUTINE (USING "LLACOPY" MACRO)
*
* SPECIFICATIONS:
*
* MODULE NAME: LLAUPDTS
*
* ATTRIBUTES : AC=1
*              RMODE 24
*              AMODE 24
*
* FUNCTION = SEE BELOW
*
*     THIS ROUTINE INVOKES THE "LLACOPY" MACRO TO SYNCHRONOUSLY
*  REFRESH AN INDIVIDUAL (MEMBER) LLA ENTRY.
*  "LLACOPY" HAS TO BE INVOKED IN SUPERVISOR STATE, KEY 0.
*  A LIST OF MEMBERS TO BE REFRESHED IS SPECIFIED IN THE "SYSIN" CARD
*  IMAGE FILE.
*
*
*  INPUT TO THE ROUTINE:
*
*    . "LLAPDS" DD SPECIFYING LLA MANAGED PDS WHICH ENTRIES
*      (MEMBERS) ARE TO BE REFRESHED.
*      IF JCL "PARM=LL" IS SPECIFIED - IT INDICATES TO THE PROGRAM
*      THAT THE LINKLIST MEMBER(S) ARE TO BE REFRESHED. IN SUCH A
*      CASE NO "LLAPDS" DD IS REQUIRED.
*
*    . SYSIN FILE (FB,80) WITH CARDS IN THE FOLLOWING FORMAT:
*      YYYYYYYY   (STARTING IN COLUMN 1)
*      "YYYYYYYY" IS THE MEMBER NAME WHICH LLA DIRECTORY ENTRY
*      NEEDS TO BE UPDATED. MEMBER NAME IS REQUIRED. ONLY ONE MEMBER
*      NAME PER "SYSIN" CARD IMAGE CAN BE SPECIFIED (THE REMAINING
*      PART OF THE CARD IMAGE IS IGNORED).
*      USE "LLAUPDTE" PROGRAM TO REFRESH THE WHOLE DIRECORY OF
*      A PDS.
*
*
*  OUTPUT:
*     FOR EACH SYSIN MEMBER NAME - A SYSPRINT LINE IS PRINTED WITH
*     A RETRUN CODE FROM THE "LLACOPY" MACRO:
*       0 - NORMAL, SUCCESSFUL COMPLETION.
*       4 - LLACOPY DID NOT FIND THE SPECIFED MEMBER IN THE "LLAPDS".
*           ENTRY FOR THAT MEMBER WAS REMOVED FROM LLA (IF PRESENT).
*       8 - I/O ERROR ACCESSING "LLAPDS", OR INSUFFICIENT VIRTUAL
*           STORAGE
*      16 - NO CONTROL CARDS/MEMBER NAMES IN SYSIN, OR, ANY DATASET
*           OPEN ERROR
*   NOTE: - IF A NON-LLA MANAGED PDS IS SPECIFIED ON LLAPDS DD, FOR
*           FOUND MEMBERS LLACOPY RETURNS RC=0. PROGRAM REPORTS A
*           RC=0 BUT, IN FACT, NO LLA DIRECTORY ENTRIES HAS BEEN
*           UPDATED.
*
*
*  SAMPLE JCL TO RUN THIS PROGRAM:
*
*         //LLAUPDTS PROC
*         //LLAUPDTS EXEC PGM=LLAUPDTS{,PARM=LL}
*         //STEPLIB    DD DISP=SHR,DSN=APF.PROG.LIB
*        {//LLAPDS     DD DISP=SHR,DSN=&LLAPDS}
*         //SYSPRINT   DD SYSOUT=*
*         //         PEND
*         //  EXEC LLAUPDTS,LLAPDS='DSNAME'
*         //SYSIN      DD *
*         MEMNAME
*         .
*         .
*         /*
*
*
* WRITTEN BY: JANEK JAKUBEK, OFD
*             APRIL     1996
*
* CHANGE ACTIVITY:
* APRIL 1/1996 JJ: THIS IS A VERSION OF THE "LLAUPDTX" PROGRAM IN WHICH
*    MEMBER NAMES ARE SPECIFIED IN (FB,80) A SEQUENTIAL SYSIN FILE.
*    80 BYTES CARD IMAGES SPECIFY THE MEMBER NAMES (MULTIPLE MEMBER
*    NAMES/SYSIN CARDS CAN BE SPECIFIED) TO BE RE-FRESHED IN A LLA
*    MANAGED DATA SET SPECIFIED IN "LLAPDS" DD.
*    A SINGLE MEMBER NAME PER SYSIN CARD IMAGE SHOULD BE SPECIFIED
*    STARTING IN COLUMN 1.
*
* REGISTERS EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RDCB     EQU   R9                 DCB ADDRESS REGISTER
RCVT     EQU   R8                 CVT ADDRESS REGISTER
         SPACE
*   BITS EQUATES
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
         SPACE
         PRINT NOGEN
LLAUPDTS CSECT                    CSECT NAME
LLAUPDTS AMODE 24
LLAUPDTS RMODE 24
         USING LLAUPDTS,R15
         SAVE  (14,12)            SAVE REGISTERS
         CNOP  0,4                ALIGNEMENT ON A FULLWORD BOUNDARY
         BAL   R2,SAE             SAVE AREA ADDRESS
SA       DS    18F                SAVE AREA
         USING SA,R2
SAE      ST    R13,SA+4           CHAIN
         ST    R2,8(R13)          SAVE AREAS
         LR    R13,R2             CURRENT SAVE AREA
         DROP  R2
         USING SA,R13             PROGRAM BASE REGISTER
         SPACE
         L     R1,0(,R1)          LOAD ADDRESS OF THE PARM FIELD
         CLC   0(2,R1),=H'0'      IS LL PARM SPECIFIED................?
         BE    NOPARM             NO
         CLC   0(2,R1),=H'2'      IS LL PARM SPECIFIED................?
         BNE   PARMINV            NO, PARM EITHER TOO SHORT OR TOO LONG
         MVC   LL,2(R1)           SAVE THE LINKLIST INDICATOR
         CLC   LL,=C'LL'          IS THE LINKLIST INDICATOR VALID.....?
         BE    NOPARM             YES, CONTINUE----------------------->
PARMINV  WTO   'LLAUPDTS - INVALID JCL PARM, IGNORED'
         SPACE
NOPARM   DS    0H
         MVC   MNAME,=CL8'LLAUPDTS' PROGRAM NAME FOR ANY OPEN ERR MSGS
         OPEN  (MSGFILE,(OUTPUT)) OPEN DIAGNOSTIC MESSAGE FILE
         TM    MSGFILE+48,DCBOFOPN    OPEN SUCCESSFUL    .............?
         BNO   MSGOPNER           NO, OPEN ERROR         ------------->
         OPEN  (SYSIN,(INPUT))    OPEN CONTROL INPUT FILE
         TM    SYSIN+48,DCBOFOPN      OPEN SUCCESSFUL    .............?
         BNO   SINOPNER           NO, OPEN ERROR         ------------->
* CHECK IF LINKLIST MEMBER(S) UPDATE
         CLC LL,=C'LL'            LINKLST UPDATE .....................?
         BNE   NOTLNKL            NO, LOAD OUR DCB ADDRESS------------>
* A LNKLST MEMBER UPDATE, USE LINK LIST DCB ADDRESS
         L     RCVT,16            LOAD CVT ADDRESS
         USING CVT,RCVT           SET CVT ADDRESSABILITY
         L     RDCB,CVTLINK       LOAD LNKLST DCB ADDRESS
         B     LLACOPY            ISSUE LLACOPY MACRO
NOTLNKL  LA    RDCB,LLAPDS        LOAD OUR DCB ADDRESS
         OPEN  (LLAPDS,(INPUT))   DCB FOR LLACOPY HAS TO BE OPENED
         TM    LLAPDS+48,DCBOFOPN     OPEN SUCCESSFUL    .............?
         BNO   PDSOPNER           NO, OPEN ERROR         ------------->
         SPACE
*\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\////////////////////////////////////*
* THE MAIN SYSIN READ/PROCESS PART
LLACOPY  GET   SYSIN,INREC        GET INPUT MEMBER NAME
         MVI   ANYINREC,C'Y'      SET FLAG INDICATING INPUT WAS PRESENT
         MVC   MEMNAME,MNAME      MOVE MEMBER NAME INTO BLDL LIST
* PREPARE THE DIAGNOSTIC MSG TEXT AREA
         MVC   MSGSEP,=C' - '     A SEPARATOR
         MVI   MSGTXT,C' '        CLEAR THE MSGTXT AREA
         MVC   MSGTXT+1(L'MSGTXT-1),MSGTXT
* ENTER SUPERVISOR STATE, KEY 0 FIRST
*
         MODESET MODE=SUP,KEY=ZERO
         LLACOPY BLDLLIST=BLDL,DCB=(RDCB),                             +
               RETCODE=RETNCODE,RSNCODE=RSONCODE
         MODESET MODE=PROB,KEY=NZERO   RESTORE THE PROBLEM STATE
* CHECK RETURN CODES FROM THE LLACOPY
         L     R15,RETNCODE       LOAD RETURN CODE FROM LLACOPY
         C     R15,RETCODE        HANDLE PROGRAM RETURN CODE
         BNH   RCHECK0            NEED NOT BE UPDATED----------------->
         ST    R15,RETCODE        UPDATE WITH A HIGHER RETURN CODE
RCHECK0  C     R15,=F'8'          CHECK IF A KNOWN CODE...............?
         BNH   RCHECK(R15)        DISCRIMINATE RC 0,4,8---------------
         B     RCUNKNWN           UNKNOWN RETURN CODE----------------->
RCHECK   B     LLARC0             SUCCESSFUL LLA UPDATE--------------->
         B     LLARC4             MEMBER NOT FOUND OR DELETED--------->
         B     LLARC8             I/O ERROR OR INSUFFICIENT STORAGE--->
         SPACE
LLARC0   MVC   MSGTXT(L'MSGRC0),MSGRC0
         B     PUTDIAGM           PRINT A COMFORTING MESSAGE
         SPACE
LLARC4   MVC   MSGTXT(L'MSGRC4),MSGRC4
         B     PUTDIAGM           PRINT A COMFORTING MESSAGE (NOT SO)
         SPACE
LLARC8   L     R15,RSONCODE       LOAD LLACOPY REASON CODE
         C     R15,=F'4'          CHECK IF A KNOWN CODE...............?
         BNH   RC8RCK(R15)        DISCRIMINATE RSC 0,4 --------------->
         B     RC8NKNR            UNKNOWN REASON CODE----------------->
RC8RCK   B     RC8RS0             I/O ERROR DOING DIRECTORY SEARCH---->
         B     RC8RS4             INSUFFICIENT STORAGE IN PRIMARY AS-->
RC8RS0   MVC   MSGTXT(L'MSGRC80),MSGRC80
         B     PUTDIAGM           PRINT A COMFORTING MESSAGE (NOT SO)
RC8RS4   MVC   MSGTXT(L'MSGRC84),MSGRC84
         B     PUTDIAGM           PRINT A COMFORTING MESSAGE (NOT SO)
* FOR RC=8, UNKNOWN REASON CODE IS IN R15
RC8NKNR  BAL   R14,CONVHEX        CONVERT R15 TO HEXA
         MVC   MSGRC8XR,HX8BYTE+4 REASON CODE INTO ERRO MSG TXT
         MVC   MSGTXT(L'MSGRC8X+L'MSGRC8XR),MSGRC8X MOVE MSG TEXT
         B     PUTDIAGM           PRINT A COMFORTING MESSAGE (NOT SO)
         SPACE
* UNKNOWN RETURN CODE FROM LLACOPY
* RETURN CODE IS IN R15 ON ENTRY
RCUNKNWN BAL   R14,CONVHEX        CONVERT R15 TO HEXA
         MVC   MSGUKNRC,HX8BYTE+4 RETURN CODE INTO ERRO MSG TXT
         L     R15,RSONCODE       LOAD REASON CODE INTO R15
         BAL   R14,CONVHEX        CONVERT R15 TO HEXA
         MVC   MSGUKNRS,HX8BYTE+4 REASON CODE INTO ERRO MSG TXT
         MVC   MSGTXT(L'MSGUKNC),MSGUKNC MOVE MSG TEXT
         B     PUTDIAGM           PRINT A COMFORTING MESSAGE (NOT SO)
         SPACE
* DIAGNOSTIC MESSAGE PRINT
PUTDIAGM DS    0H                 PUT OUT A DIAGNOSTIC MESSAGE
         PUT   MSGFILE,MSGREC     WRITE OUT A DIAGNOSTIC MESSAGE
         B     LLACOPY            PROCESS NEXT SYSIN RECORD
*\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\////////////////////////////////////*
         SPACE
* WE HAVE EOD ON SYSIN
EOF      CLI   ANYINREC,C'Y'      SYSIN INPUT WAS PRESENT
         BE    EOFN               YES, NORMAL END
         MVC   RETCODE,=F'16'     NO, SET RET.CODE=16
         MVC   MSGTXT(L'MSGNSIN),MSGNSIN NO SYSIN INPUT ERROR MSG
         PUT   MSGFILE,MSGREC     WRITE OUT A DIAGNOSTIC MESSAGE
EOFN     CLC   LL,=C'LL'          LINKLST UPDATED.....................?
         BE    LLACPYX            YES, NO CLOSE REQUIRED-------------->
         CLOSE (LLAPDS)           NO, CLOSE THE DCB
LLACPYX  CLOSE (SYSIN,,MSGFILE)   CLOSE SYSIN AND DIAGNOSTIC MSG FILE
         L     R15,RETCODE        LOAD END OF RUN RETURN CODE
RET      L     R13,SA+4           PREVIOUS SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)
         SPACE
RC16P    PUT   MSGFILE,MSGREC     WRITE OUT A DIAGNOSTIC MESSAGE
RC16     LA    R15,16             UNSUCCESSFUL COMPLETION
         B     RET
         SPACE
MSGOPNER DS    0H                 DIAGNOSTIC ERROR MSG FILE COULD
*                                 NOT BE OPENED
         WTO   'LLAUPDTS - SYSPRINT OPEN ERROR, RC=16'
         B     RC16
         SPACE
SINOPNER DS    0H                 SYSIN OPEN ERROR
         MVC   MSGTXT(L'MSGSINOE),MSGSINOE SYSIN OPEN ERROR MSG
         B     RC16P              PRINT ERR MSG AN EXIT WITH RC=16
         SPACE
PDSOPNER DS    0H                 LLAPDS OPEN ERROR
         MVC   MSGTXT(L'MSGPDSOE),MSGPDSOE LLAPDS OPEN ERROR MSG
         B     RC16P              PRINT ERR MSG AN EXIT WITH RC=16
*---------------------------------------------------------------------*
*  THIS ROUTINE CONVERTS R15 TO CHARACTER HEXA DIGITS
*  RETURN THROUGH R14
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
CONVHEX  ST    R15,HX4BYTE           STORE ERROR CODE IN A WORK FIELD
         UNPK  HX9BYTE,HX5BYTE
         NC    HX8BYTE,=8X'0F'       CLEAR 4 BITS OF EACH BYTE
         TR    HX8BYTE,=C'0123456789ABCDEF'
         BR    R14                   RETURN TO THE CALLER
*
HX5BYTE  DS    0CL5                  'PACKED' DATA
HX4BYTE  DS    CL4                   BINARY ERROR CODE
         DS    C                     LAST BYTE TO UNPACK
HX9BYTE  DS    0CL9                  'UNPACKED' DATA
HX8BYTE  DS    CL8                   CHARACTER ERROR CODE
         DS    C                     EXTRANOUS DATA
*---------------------------------------------------------------------*
         SPACE
*  LLAPDS DATA SET
LLAPDS   DCB   DSORG=PO,MACRF=R,DDNAME=LLAPDS
*  DIAGNOSTIC MESSAGE MESSAGE FILE
MSGFILE  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,            +
               LRECL=(L'MSGREC),BLKSIZE=(L'MSGREC*33)
*  CONTROL (MEMBER NAMES TO BE REFRESHED) FILE
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,RECFM=FB,LRECL=(L'INREC),+
               EODAD=EOF
         SPACE
LL       DC    CL2'XX'            LL FROM JCL PARM
RETNCODE DS    F                  RETURN CODE FROM LLACOPY MACRO
RSONCODE DS    F                  REASON CODE FROM LLACOPY MACRO
RETCODE  DC    F'0'               RETURN CODE FROM THE PROGRAM
*                                 (HIGHEST RETURNED FROM LLACOPY MACRO)
*//////////////////////////////////////////////////////////////////////
BLDL     DS    0F                 BLDL LIST TO FOR LLACOPY
         DC    AL2(1)             NUMBER OF ENTRIES
         DC    AL2(12)            ENTRY LENGTH (MINIMUM IS 12 BYTES)
MEMNAME  DS    CL8                MEMBER NAME TO BE REFRESHED IN LLA
         DS    CL4                TTR,K FIELDS TO BE RETURNED
*//////////////////////////////////////////////////////////////////////
*
*  DIAGNOSTIC MESSAGES
*
*//////////////////////////////////////////////////////////////////////
MSGREC   DS    0CL121             121 BYTES LONG
MSGASA   DC    C'0'               PRINT LINES SEPARATED BY 1 BLANK LINE
INREC    DS    0CL80
MNAME    DC    CL8' '             INPUT MEMBER NAME
MSGSEP   DC    C' - '             A SEPARATOR
MSGTXT   DC    CL(121-12)' '      DIAG MSG TEXT
*//////////////////////////////////////////////////////////////////////
ANYINREC DC    X'00'              A FLAG INDICATING SYSIN INPUT PRESENT
MSGRC0   DC    C'LLACOPY RC=0, SUCCESSFUL REFRESH'
MSGRC4   DC    C'LLACOPY RC=4, MEMBER NOT FOUND IN LLAPDS/LINKLST'
MSGRC80  DC    C'LLACOPY RC=8, LLAPDS DIR SEARCH I/O ERROR'
MSGRC84  DC    C'LLACOPY RC=8, INSUFFICIENT VIRTUAL STORAGE'
*//////////////////////////////////////////////////////////////////////
MSGRC8X  DC    C'LLACOPY RC=8, UNKNOWN REASON CODE='
MSGRC8XR DC    C'XXXX'              REASON CODE FROM LLACOPY
*//////////////////////////////////////////////////////////////////////
*//////////////////////////////////////////////////////////////////////
MSGUKNC  DS    0CL(MSGUKNE-MSGUKNC1) UNKONWN RETURN CODE FROM LLACOPY
MSGUKNC1 DC    C'LLACOPY RET.CODE='
MSGUKNRC DC    C'XXXX'           RETURN CODE FROM LLACOPY
         DC    C', RSN.CODE='
MSGUKNRS DC    C'XXXX'           REASON CODE FROM LLACOPY
MSGUKNE  EQU   *
*//////////////////////////////////////////////////////////////////////
MSGNSIN  DC    C'NO INPUT ON SYSIN, RC=16'
MSGSINOE DC    C'SYSIN OPEN ERROR, RC=16'
MSGPDSOE DC    C'LLAPDS OPEN ERROR, RC=16'
         LTORG
         SPACE
         PRINT NOGEN
         CVT   DSECT=YES          CVT MAPPING MACRO
         SPACE
         DCBD  DSORG=PS
         END   LLAUPDTS
/*
//*
//LKED     EXEC PGM=IEWL,
//         PARM='NCAL,LET,LIST,XREF,AC=1'
//SYSLMOD  DD DISP=SHR,DSN=...YOUR.APF.LOADLIB....
//SYSUT1   DD UNIT=VIO,SPACE=(TRK,(10,10))
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=&&SYSLIN,DISP=(OLD,DELETE)
//         DD DDNAME=SYSIN
//SYSIN    DD *
 NAME LLAUPDTS(R)
/*
