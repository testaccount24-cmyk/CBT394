/* REXX    */
/* Written by: Janek Jakubek, CGI MDC                              */
/*             December 2002                                       */
/*                                                                 */
/* Function:                                                       */
/* Make all address spaces with same name nonswappable. This can be*/
/* useful for Unix applications (in our case CTG).                 */
/* Does this via MXI MAKE command.                                 */
/*                                                                 */
/* Has to run in a TSO environment (batch or foreground)           */
/* because MXI MAKE function is authorized. Will not work          */
/* if invoked via IRXJCL.                                          */
/* Also, the user that runs this has to be authorized to the       */
/* FACILITY class resource MXICMD.MAKE .                           */
/*                                                                 */
/* Syntax: %MAKENSWP jobname                                       */
/*                                                                 */
/* TRACE A */
parse upper arg jobname
msgid = SYSVAR('SYSICMD')
/*                                                                 */
/* JOBNAME has to be specified.                                    */
/* We do not allow generic jobnames to prevent mistakes            */
/* (like making all AS on a system non-swappable).                 */
/*                                                                 */
say msgid 'Requested jobname:' jobname
If jobname = '' then
  do
  say msgid 'JOBNAME has to be specified.'
  exit   16;
  end
/* We do not allow generic jobnames to avoid errors                */
If verify(jobname,'*%','Match') > 0 then
  do
  say msgid jobname '<--- generic jobname is not allowed'
  exit   16;
  end
/*                                                                 */
dacmd = 'DA 'jobname
say msgid 'Issuing command:' dacmd
rcode0 = MXIREXX('outline1.','NOTITLES',dacmd)
If  (rcode0 = 0  & outline1.0 >= 1) then
  do
  If (outline1.0 >= 31 & substr(outline1.1,1,14) = 'Identification')
  then    /* We seem to have a single address space display */
    do
    loopcnt = 1
    asidxpos = 58    /* ASID (hex) position on the display    */
    i = 2            /* DA display line # to look at          */
    end
  else               /* We seem to have a multi-AS DA display */
    do               /* (one line for an AS)                  */
    i = 1
    asidxpos = 39    /* ASID (hex) position on the display    */
    loopcnt = outline1.0
    end
  do loopcnt
    say outline1.i
    asidx = substr(outline1.i,asidxpos,4)
    makecmd = 'MAKE A='asidx' SWAP(NO)'
    say msgid 'Issuing command:' makecmd
    rcode1 = MXIREXX('outline2.',makecmd)
    if rcode1 Â¬= 0
    then say msgid 'MAKE command failed. Return Code:' rcode1
    i = i + 1
  end
  end
else
  do
  say msgid 'DA 'jobname' command failed. Return Code:' rcode0
  say msgid 'Number of lines returned by DA:' outline1.0
  if outline1.0 = 0 then
    say msgid 'Job' jobname 'is not active/ running.'
  end
exit
