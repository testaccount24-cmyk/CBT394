         MACRO
&NAME    HLVTD &DSECT,            IF='DSECT' DSECT WILL BE GENERATED   +
               &PREF=,            FIELDS NAMES PREFIX                  +
               &SUFF=,            FIELDS NAMES SUFFIX                  +
               &LEN=,             TABLE ELEMENT LENGTH                 +
               &OFFS=,            TABLE ELEMENTS OFFSET                +
               &KL=*,             CHARACTER KEY LENGTH                 +
               &CMPRE=*,          COMPARE EXIT ROUTINE ADDRESS         +
               &NM=,              MAX NO. OF ELEMENTS IN THE TABLE     +
               &NA=               CURRENT NO. OF ELEMENTS IN THE TABLE
         AIF   ('&PREF' NE '' AND '&SUFF' NE '').PREFSUF
         AIF   (K'&PREF GT 3 OR K'&SUFF GT 3).LPRESUF
         AIF   ('&KL' NE '*' AND '&CMPRE' NE '*').KLMPREX
         AIF   ('&DSECT' NE 'DSECT').NODSECT
&NAME    DSECT                    TABLE DEFINITION FOR "HALVE" SUBROUT.
         AGO   .NODS
.NODSECT ANOP
&NAME    DS    0F                 TABLE DEFINITION FOR "HALVE" SUBROUT.
.NODS    ANOP
         LCLC  &LABEL
&LABEL   SETC  '&PREF.SA&SUFF'
&LABEL   DS    20A                SAVE AREA FOR HALVE (REENTRANCE)
&LABEL   SETC  '&PREF.FEA&SUFF'
&LABEL   DS    A                  TABLE ( FIRST ELEMENT ) ADDRESS
&LABEL   SETC  '&PREF.LEN&SUFF'
         AIF   ('&LEN' NE '').ILEN
&LABEL   DS    H                  TABLE ELEMENT LENGTH
         AGO   .OFF
.ILEN    ANOP
&LABEL   DC    AL2(&LEN)          TABLE ELEMENT LENGTH
.OFF     ANOP
&LABEL   SETC  '&PREF.OFFS&SUFF'
         AIF   ('&OFFS' NE '' OR '&LEN' NE '').IOFF
&LABEL   DS    H                  OFFSET OF TWO ADJACENT ELEM.OF TABLE
         AGO   .LCMPRE
.IOFF    AIF   ('&OFFS' EQ '').IOFF1
&LABEL   DC    AL2(&OFFS)        OFFSET OF TWO ADJACENT ELEM.OF TABLE
         AGO   .LCMPRE
.IOFF1   ANOP
&LABEL   DC    AL2(&LEN)          OFFSET OF TWO ADJACENT ELEM.OF TABLE
.LCMPRE  ANOP
         AIF   ('&CMPRE' NE '*').CMPRE
&LABEL   SETC  '&PREF.KL&SUFF'    NO COMPARE EXIT SPECIFIED
         AIF   ('&KL' NE '' AND '&KL' NE '*').IKL
&LABEL   DS    F                  CHARACTER KEY LENGTH
         AGO   .NM
.IKL     ANOP
&LABEL   DC    A(&KL)             CHARACTER KEY LENGTH
         AGO   .NM
.CMPRE   ANOP
&LABEL   SETC  '&PREF.CMPRE&SUFF'
         AIF   ('&CMPRE' NE '').ICMPRE
&LABEL   DS    A                  ADDRESS OF ADDRESS OF ENTRY POINT OF +
                                  COMPARE EXIT ROUTINE
         AGO   .NM
.ICMPRE  ANOP
&LABEL   DC    A(&CMPRE)          ADDRESS OF ADDRESS OF ENTRY POINT OF +
                                  COMPARE EXIT ROUTINE
.NM      ANOP
&LABEL   SETC  '&PREF.NM&SUFF'
         AIF   ('&NM' NE '').INM  INITIAL VALUE SPECIFIED FOR NM
&LABEL   DS    F                  MAX. NO. OF ELEMENTS IN THE TABLE
         AGO   .NA
.INM     ANOP
&LABEL   DC    A(&NM)             MAX. NO. OF ELEMENTS IN THE TABLE
.NA      ANOP
&LABEL   SETC  '&PREF.NA&SUFF'
         AIF   ('&NA' NE '' OR '&NM' NE '').INA
&LABEL   DS    F                  CURRENT NO. OF ACTIVE ELEM.IN TABLE
         AGO   .EADR
.INA     AIF   ('&NA' NE '').INA1 NA SECIFIED
&LABEL   DC    A(&NM)             CURRENT NO. OF ACTIVE ELEM.IN TABLE
         AGO   .EADR
.INA1    ANOP
&LABEL   DC    A(&NA)             CURRENT NO. OF ACTIVE ELEM.IN TABLE
.EADR    ANOP
&LABEL   SETC  '&PREF.EADR&SUFF'
&LABEL   DS    A                  LOCATED ELEM.ADDR.RETURNED BY HALVE
&LABEL   SETC  '&PREF.EPOS&SUFF'
&LABEL   DS    F                  POSITION IN THE TABLE OF LOCATED ELEM.
                                  OR ZERO IF TABLE CANNOT BE EXPANED
         AGO   .END
.PREFSUF MNOTE 8,'***** HLVTD ** "PREF" AND "SUFF" ARE MUTUALLY EXCLUSI+
               VE OPERANDS. DO NOT SPECIFY THEM BOTH AT THE SAME TIME'
         AGO   .END
.LPRESUF MNOTE 8,'***** HLVTD ** LENGTH OF "PREF" AND "SUFF" CANNOT EXC+
               EED 3 CHARACTERS'
         AGO   .END
.KLMPREX MNOTE 8,'***** HLVTD ** "KL" AND "CMPRE" ARE MUTUALLY EXCLUSIV+
               E OPERANDS. DO NOT SPECIFY THEM BOTH AT THE SAME TIME'
.END     MEND
